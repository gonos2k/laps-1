SUBROUTINE READ_SYSTIM(DDMMYYHM,TIMEOFCH,YYMMDDHM,EXSTATUS)

!==============================================================================
!  THIS ROUTINE DECODES AN ASCII TIME STRING INTO INTEGERS OF DAY, MONTH, YEAR,
!  HOUR, MINUTE.
!
!  HISTORY:
!	CREATION:	YUANFU XIE	JUN 2007
!==============================================================================

  IMPLICIT NONE

  CHARACTER, INTENT(IN)  :: DDMMYYHM*16
  CHARACTER, INTENT(OUT) :: TIMEOFCH*14
  INTEGER,   INTENT(OUT) :: YYMMDDHM(5)
  INTEGER,   INTENT(OUT) :: EXSTATUS

  ! LOCAL VARIABLES
  INTEGER :: ZERO
  INTEGER :: IMON
  CHARACTER(LEN=3), PARAMETER :: MONTH_ABBR(12) = &
       (/ 'JAN','FEB','MAR','APR','MAY','JUN', &
          'JUL','AUG','SEP','OCT','NOV','DEC' /)

  ZERO = ICHAR('0')
  EXSTATUS = 0

  ! SECOND:
  TIMEOFCH(13:14) = '00'

  ! DAY:
  YYMMDDHM(3) = (ICHAR(DDMMYYHM(1:1))-ZERO)*10+ICHAR(DDMMYYHM(2:2))-ZERO
  TIMEOFCH(7:8) = DDMMYYHM(1:2)

  ! MONTH using array lookup instead of long SELECT
  YYMMDDHM(2) = 0
  DO IMON = 1, 12
    IF (DDMMYYHM(4:6) == MONTH_ABBR(IMON)) THEN
      YYMMDDHM(2) = IMON
      WRITE(TIMEOFCH(5:6),'(I2.2)') IMON
      EXIT
    END IF
  END DO

  ! YEAR:
  YYMMDDHM(1) = (ICHAR(DDMMYYHM(8 : 8))-ZERO)*1000+ &
                (ICHAR(DDMMYYHM(9 : 9))-ZERO)*100 + &
                (ICHAR(DDMMYYHM(10:10))-ZERO)*10+ &
                 ICHAR(DDMMYYHM(11:11))-ZERO
  TIMEOFCH(1:4) = DDMMYYHM(8:11)

  YYMMDDHM(4) = (ICHAR(DDMMYYHM(13:13))-ZERO)*10+&
                 ICHAR(DDMMYYHM(14:14))-ZERO
  TIMEOFCH(9:10) = DDMMYYHM(13:14)

  YYMMDDHM(5) = (ICHAR(DDMMYYHM(15:15))-ZERO)*10+&
                 ICHAR(DDMMYYHM(16:16))-ZERO
  TIMEOFCH(11:12) = DDMMYYHM(15:16)

END SUBROUTINE READ_SYSTIM
